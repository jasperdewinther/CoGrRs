#version 450
#extension GL_GOOGLE_include_directive : require

struct BVHNode{
    float minx;
    float miny;
    float minz;
    float maxx;
    float maxy;
    float maxz;
    int left_first;
    int count;
};

struct StackNode{
    uint bvh_node_index;
    float dist;
};

layout(rgba8, binding=0) uniform image2D to_draw_texture;
layout(std430, binding=1) buffer vertices_block { vec4 vertices[]; };
layout(std430, binding=2) buffer triangles_block { uvec4 triangles[]; };
layout(std430, binding=3) buffer indices_block { uint indices[]; };
layout(std430, binding=4) buffer bvh_nodes_block { BVHNode bvh_nodes[]; };
layout(local_size_x = 32, local_size_y = 32, local_size_z = 1) in;

layout(push_constant) uniform constants
{
    vec4 camera_dir;
    vec4 camera_pos;
    vec4 camera_side;
    vec4 camera_up;
    float width;
    float half_width;
    float height;
    float half_height;
    float time;
    uint padding1;
    uint padding2;
    uint padding3;
} gpu_data;

#define FLT_MAX 3.402823466e+38

vec4 my_cross(vec4 a, vec4 b){
    return vec4(  
        a.y * b.z - a.z * b.y,
        a.z * b.x - a.x * b.z,
        a.x * b.y - a.y * b.x,
        0
    );
}


void intersect_ray_triangle(
    in vec4 ray_o, 
    in vec4 ray_dir, 
    inout float ray_t, 
    inout uint prim_id,
    in uint triangle_id
){
    vec4 p1 = vertices[triangles[triangle_id].x];
    vec4 p2 = vertices[triangles[triangle_id].y];
    vec4 p3 = vertices[triangles[triangle_id].z];
    vec4 p1_to_p2 = p2 - p1;
    vec4 p1_to_p3 = p3 - p1;
    vec4 uvec = my_cross(ray_dir, p1_to_p3);
    float det = dot(p1_to_p2, uvec);
    float inv_det = 1/det;
    vec4 a_to_origin = ray_o - p1;
    float u = dot(a_to_origin, uvec) * inv_det;
    if (u < 0 || u > 1){
        return;
    }
    vec4 vvec = my_cross(a_to_origin, p1_to_p2);
    float v = dot(ray_dir, vvec) * inv_det;
    if (v < 0 || u + v > 1){
        return;
    }
    float dist = dot(p1_to_p3, vvec) * inv_det;
    if (dist > 0.00000001 && dist < ray_t){
        ray_t = dist;
        prim_id = triangle_id;
    }
}

float intersect_ray_aabb(
    in vec4 ray_o, 
    in vec4 ray_dir, 
    in vec4 ray_dirr, 
    inout float ray_t, 
    inout uint prim_id,
    in uint aabb_id
){
    BVHNode node = bvh_nodes[aabb_id];
    vec4 v_max = vec4(node.maxx,node.maxy, node.maxz, 0); 
    vec4 v_min = vec4(node.minx, node.miny, node.minz, 0); 
    vec4 tMin  = (v_min - ray_o)*ray_dirr;
    vec4 tMax = (v_max - ray_o)*ray_dirr;
    vec4 t1 = min(tMin, tMax);
    vec4 t2 = max(tMin, tMax);
    float tNear = max(max(t1.x, t1.y), t1.z);
    float tFar = min(min(t2.x, t2.y), t2.z);
    if (tFar >= tNear && tNear < ray_t && tFar > 0){
        return tNear;
    } else {
        return FLT_MAX;
    }
}

void traverse(
    in vec4 ray_o, 
    in vec4 ray_dir, 
    in vec4 ray_dirr, 
    inout float ray_t, 
    inout uint prim_id
){
    StackNode stack[32];
    uint node_index = 0;
    uint stack_ptr = 0;
    while(true){
        if (bvh_nodes[node_index].count > 0){
            for (int i = 0; i < bvh_nodes[node_index].count; i++){
                intersect_ray_triangle(
                    ray_o, 
                    ray_dir, 
                    ray_t, 
                    prim_id,
                    indices[bvh_nodes[node_index].left_first + i]
                );
            }
            if (stack_ptr == 0){
                break;
            } else {
                float t = FLT_MAX;
                while (t >= ray_t){
                    if (stack_ptr == 0){
                        return;
                    }
                    stack_ptr-=1;
                    StackNode sn = stack[stack_ptr];
                    t = sn.dist;
                    node_index = sn.bvh_node_index;
                }
                continue;
            }
        }
        uint child1 = bvh_nodes[node_index].left_first;
        uint child2 = bvh_nodes[node_index].left_first + 1;

        float dist1 = intersect_ray_aabb(
            ray_o, 
            ray_dir, 
            ray_dirr, 
            ray_t, 
            prim_id,
            child1
        );
        float dist2 = intersect_ray_aabb(
            ray_o, 
            ray_dir, 
            ray_dirr, 
            ray_t, 
            prim_id,
            child2
        );
        if (dist1 > dist2){
            uint tempu = child1;
            child1 = child2;
            child2 = tempu;
            float tempf = dist1;
            dist1 = dist2;
            dist2 = tempf;
        }
        if (dist1 == FLT_MAX){
            if (stack_ptr == 0){
                return;
            } else {
                float t = FLT_MAX;
                while (t >= ray_t){
                    if (stack_ptr == 0){
                        return;
                    }
                    stack_ptr-=1;
                    StackNode sn = stack[stack_ptr];
                    t = sn.dist;
                    node_index = sn.bvh_node_index;
                }
            }
        } else {
            node_index = child1;
            if (dist2 != FLT_MAX){
                stack[stack_ptr] = StackNode(child2, dist2);
                stack_ptr += 1;
            }
        }
    }
}

vec4 triangle_normal(uint triangle_id){
    vec4 p1 = vertices[triangles[triangle_id].x];
    vec4 p2 = vertices[triangles[triangle_id].y];
    vec4 p3 = vertices[triangles[triangle_id].z];
    p1 = p2 - p1;
    p2 = p2 - p3;
    return normalize(my_cross(normalize(p1), normalize(p2)));
}

void main() {
    uvec3 global_invocation_id = gl_GlobalInvocationID;
    uint x = global_invocation_id.x;
    uint y = global_invocation_id.y;
    ivec2 pos = ivec2(x,y);

    vec4 dir = gpu_data.camera_pos
        + gpu_data.camera_dir
        + gpu_data.camera_side * (float(x) - gpu_data.half_width)
            / (gpu_data.width / ( gpu_data.width / gpu_data.height))
        + gpu_data.camera_up * (float(y) - gpu_data.half_height) /  gpu_data.height;

    dir = normalize(dir - gpu_data.camera_pos);
    vec4 dirr = 1/dir;
    float t = FLT_MAX;
    uint prim = -1;

    /*for (int i = 0; i < 6320; i++){
        intersect_ray_triangle(
            gpu_data.camera_pos, 
            dir, 
            t, 
            prim,
            i
        );
    }*/
    
    traverse(
        gpu_data.camera_pos, 
        dir, 
        dirr, 
        t, 
        prim
    );
    

    if (prim != uint(-1)){
        vec4 normal = triangle_normal(prim);
        vec4 color = vec4(dot(normal.xyz, normalize(vec3(1,-1,1)))+1)/2;
        imageStore(to_draw_texture, pos, color);
    } else {
        imageStore(to_draw_texture, pos, vec4(0));
    }
}

